<html>
<head>
<title>webgl demo</title>

<script src="https://raw.github.com/mrdoob/three.js/master/build/three.js"></script>
<script src="http://code.jquery.com/jquery-latest.min.js"></script>
<script src="webgl.js"></script>

<script id="vertexShader" type="x-shader/x-vertex">
    varying vec2 merc;

    void main() {
        merc = uv;
        gl_Position = projectionMatrix * 
                      modelViewMatrix * 
                      vec4(position, 1.0);
    }
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
    #define PI  3.1415926535
    #define PI2 6.2831853071

    // lat/lon degrees
    uniform vec2 pole;
    // pixels per earth circumference (undistorted)
    uniform float scale;
    // overzoom is capped at 2^bias
    uniform float bias;

    // projected mercator unit coordinates: lon:[-180,180] => x:[0,1], lat:0 => y:0
    varying vec2 merc;

    void llr_to_xyz(in vec2 llr, out vec3 xyz) {
        xyz = vec3(cos(llr.s) * cos(llr.t), sin(llr.s) * cos(llr.t), sin(llr.t));
    }

    void xyz_to_llr(in vec3 xyz, out vec2 llr) {
        // don't use asin(z) because of precision issues
        llr = vec2(atan(xyz.y, xyz.x), atan(xyz.z, length(vec2(xyz.x, xyz.y))));
    }

    // remap lat/lon llr to llr_trans, shifting lat/lon 90,0 to coordinates of fake_pole
    void translate_pole(in vec2 llr, in vec2 fake_pole, out vec2 llr_trans) {
        vec2 rpole = radians(fake_pole);
        vec3 xyz;
        llr_to_xyz(llr, xyz);

        float latrot = rpole.t - .5 * PI;
        float lonrot = rpole.s;
        vec3 trans_xyz = mat3( // second, shift (pole lat, 0) to (pole lat, pole lon)
          cos(lonrot), sin(lonrot), 0,
          -sin(lonrot), cos(lonrot), 0,
          0, 0, 1
        ) * mat3( // first, shift lat +90 to pole lat
          cos(latrot), 0, sin(latrot),
          0, 1, 0,
          -sin(latrot), 0, cos(latrot)
        ) * xyz;
        xyz_to_llr(trans_xyz, llr_trans);
    }

    void main() {
        // projected mercator radians: lon:[-180:180] => x:[-pi, pi]
        vec2 merc_rad = (merc + vec2(-.5, 0)) * PI2;
        // geographic coordinates, radians
        vec2 geo_rad = vec2(merc_rad.s, 2. * atan(exp(merc_rad.t)) - .5 * PI);

        // do pole transformation here
        vec2 abs_geo_rad;
        translate_pole(geo_rad, pole, abs_geo_rad);

        // projected mercator coordinates, radians
        vec2 abs_merc_rad = vec2(abs_geo_rad.s, log(tan(.5 * abs_geo_rad.t + .25 * PI)));
        mat3 merc_map_tx = mat3(
          1./PI2, 0, 0,
          0, -1./PI2, 0,
          .5, .5, 1
        );
        // projected mercator quadtree-map coordiantes: lon:[-180,180] => x:[0,1], lat:[-90,90] => y:[+inf,-inf]
        vec2 abs_map = vec2(merc_map_tx * vec3(abs_merc_rad.s, abs_merc_rad.t, 1.));
        abs_map.x = mod(abs_map.x, 1.);

        bool oob = (abs_map.t < 0. || abs_map.t >= 1.);


    /*
        vec2 k = abs_map;
        float z;
        if (k.t < 0. || k.t > 1.) {
          z = .7;
        } else {
          z = 1.;
        }

        gl_FragColor = vec4(z, k.s, k.t, 1.);
*/

        if (oob) {
          gl_FragColor = vec4(.7, .7, .7, 1.);
        } else {
          float res = PI2 / scale * cos(geo_rad.t);
          float base_res = PI2 / 256.;
          float fzoom = log2(base_res / res);
          float zoom = ceil(fzoom - bias);
          if (zoom < 0.) {
            zoom = 0.;
          }

          float tx = floor(abs_map.s * pow(2., zoom));
          float ty = floor(abs_map.t * pow(2., zoom));

          gl_FragColor = vec4(.04 * zoom, mod(tx + ty, 2.), 0, 1);
        }

    }
</script>

<script>

$(document).ready(init2);

</script>

</head>
<body>

<style>

body {
  margin: 0;
}

</style>

</body>
</html>
